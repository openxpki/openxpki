#!/usr/bin/perl -w
## CGI script for the OpenXPKI SCEP server
##
## Written by Oliver Welter OpenXPKI Project
## Based on the script from Alexander Klink
## Copyright (c) 2006-2013 by The OpenXPKI project

use strict;
use warnings;
use CGI qw( -debug );
use CGI::Carp qw( fatalsToBrowser );
use Config::Std;
use NetAddr::IP;
use English;
use OpenXPKI::Log4perl;

require OpenXPKI::Client::SCEP;
my $query = new CGI;

my $basedir ='/etc/openxpki/scep';
my $configfile = '';

# check for explicit file in env
if ($ENV{OPENXPKI_SCEP_CLIENT_CONF_FILE}) {
    -f $ENV{OPENXPKI_SCEP_CLIENT_CONF_FILE}
        || die sprintf "Config file not found (%s, from env)", $ENV{OPENXPKI_SCEP_CLIENT_CONF_FILE};
    $configfile = $ENV{OPENXPKI_SCEP_CLIENT_CONF_FILE};
} else {

    # check for basedir in env
    if ( $ENV{OPENXPKI_SCEP_CLIENT_CONF_DIR} ) {
        -d $ENV{OPENXPKI_SCEP_CLIENT_CONF_DIR}
            || die sprintf "Config directory not found (%s, from env)", $ENV{OPENXPKI_SCEP_CLIENT_CONF_DIR};
        $basedir = $ENV{OPENXPKI_SCEP_CLIENT_CONF_DIR};
        $basedir =~ s/\/$//;
    }

    # Test for specific config file based on script name
    # SCRIPT_URL is only available with mod_rewrite
    if (defined $ENV{SCRIPT_URL}) {
        $ENV{SCRIPT_URL} =~ qq|scep/([^/]+)(/[^/]*)?\$|;
        $configfile = "$basedir/$1.conf";

    # Should always work
    } elsif (defined $ENV{REQUEST_URI}) {
        $ENV{REQUEST_URI} =~ qq|scep/([^/\?]+)(/[^/\\?]*)?(\\?.*)?\$|;
        $configfile = "$basedir/$1.conf";

    # Hopefully never seen
    } else {
        die "Unable to detect script name - please check the docs";
    }

    # use default if no custom file
    $configfile = $basedir.'/default.conf' unless ( -f $configfile );

    # oops, default file also not found
    die sprintf "No config file in basedir %s for script %s", $basedir, $1 unless ( -f $configfile );
}


read_config $configfile => my %config;
my $socket  = $config{global}{socket};
my $realm   = $config{global}{realm};
my $iprange = $config{global}{iprange};
my $profile = $config{global}{profile};
my $server  = $config{global}{servername};
my $enc_alg = $config{global}{encryption_algorithm};
my $hash_alg = $config{global}{hash_algorithm};

OpenXPKI::Log4perl->init_or_fallback( $config{global}{log_config} );
my $log = Log::Log4perl->get_logger( $config{global}{log_facility} || 'scep' );

my $allowed_range = new NetAddr::IP $iprange; # the allowed IP range
                     # from the config file
my $requesting_host = new NetAddr::IP $ENV{'REMOTE_ADDR'}; # the host

$log->debug('Used configfile ' . $configfile);

# Check if requesting host is allowed to talk to us
if (!$requesting_host->within($allowed_range)) {
# TODO: better response?

    $log->info('Blocked request from ' . $requesting_host );

    print "Content-Type: text/plain\n\nGo away\n";
    die("Unauthorized access from $requesting_host");
}
else {
    # Fetch SCEP message from CGI (cf. Section 3.1 of the SCEP draft)
    # http://www.ietf.org/internet-drafts/draft-nourse-scep-13.txt
    my $operation = $query->param('operation');
    my $message   = $query->param('message');

    # Get additional parameters from the url
    my @extra_params = $query->param;

    # TODO - Whitelist, Url-Decode?
    my $params = {};
    foreach my $param (@extra_params) {
        if ($param eq "operation" ||  $param eq "message") { next; }
        $params->{$param} = $query->param($param);
    }

    # Append the remote address to the params hash
    $params->{remote_addr} = $requesting_host->addr;

    $log->info('Incoming request from ' . $requesting_host->addr . ' with ' . $operation );

    # OpenXPKI::Client::SCEP does the actual work
    my $scep_client = OpenXPKI::Client::SCEP->new(
        {
        SERVICE    => 'SCEP',
        REALM      => $realm,
        SOCKETFILE => $socket,
        TIMEOUT    => 120, # TODO - make configurable?
        PROFILE    => $profile,
        OPERATION  => $operation,
        MESSAGE    => $message,
        SERVER     => $server,
        ENCRYPTION_ALGORITHM => $enc_alg,
        HASH_ALGORITHM => $hash_alg
        });
    if (! defined $scep_client) {
        die "SCEP client not defined";
    }
    my $result = $scep_client->send_request($params);
    print $result;

}

=head1 Description

This file is the generic scep handler to be used as cgi script with your
favourite webserver (apache is tested, others are not but should work).
The script needs a config file which sets the parameters such as realm,
profile, workflow to be triggered on the OpenXPKI system on request.

To ease configuration, the script has an autodetexction feature for its
config file.

=head2 config file

The config file is parsed using Config::Std, all params are mandatory.

    [global]
    socket=/var/openxpki/openxpki.socket
    realm=ca-one
    iprange=0.0.0.0/0
    profile=I18N_OPENXPKI_PROFILE_TLS_SERVER
    servername=tls-scep-1
    encryption_algorithm=3DES

=over

=item socket

Location of the OpenXPKI socket file, the webserver needs rw access.

=item realm

The realm of the ca to be used.

=item iprange

Implements a simple ip based access control, the clients ip adress is checked
to be included in the given network. Only a single network definition is
supported, the default of 0.0.0.0/0 allows all ips to connect.

=item profile

The profile of the certificate to be requested, note that depending on the
backing workflow this might be ignored or overridden by other paramters.

=item servername

Path to the server side config of this scep service. Equal to the key from
the config section in the scep.yaml file.

=item encryption

Encrpytion to use, supported values are I<DES> and I<3DES>.

=back

=head2 config file location

=over

=item autodetected file

The default location for config files is /etc/openxpki/scep, the script
will use the filename of the called script (from ENV{SCRIPT_NAME)) and looks
for /etc/openxpki/scep/I<filename>.conf. If no file is found, the default
config is loaded from /etc/openxpki/scep/default.conf.
Note: The scriptname value respects symlinks, so you can use a single scep
handler script and create symlinks on it.

=item custom base directory

Set I<OPENXPKI_SCEP_CLIENT_CONF_DIR> to a directory path. The autodetection
will now use this path to find either the special or the default file. Note
that there is no fallback to the default location!

=item fixed file

Set I<OPENXPKI_SCEP_CLIENT_CONF_FILE> to an absolute file path.
On apache, this can be combined with location to set a config for a special
script:

   <Location /cgi-bin/scep/mailgateway>
      SetEnv OPENXPKI_SCEP_CLIENT_CONF_FILE /home/mailadm/scep.conf
   </Location>

=back

=head2 webserver setup

If you use apache, the easiest way is a directory based wildcard alias.

    ScriptAlias /scep  /usr/lib/cgi-bin/scep
    <Directory "/usr/lib/cgi-bin/">
            AllowOverride None
            Options +ExecCGI -MultiViews +SymLinksIfOwnerMatch
            Order allow,deny
            Allow from all
    </Directory>

Make your requests to C<http://server/scep/myserver> which will pull in
the config from /etc/openxpki/scep/myserver.conf.

Note: SCEP usually uses HTTP/1.0, so name based virtual hosts are not working.
